plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java-gradle-plugin' // ðŸ”¥ required for plugin publishing
}

group = 'org.mangorage'
version = '1.3.0'

configurations {
    shadow
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly gradleApi()
    compileOnly 'com.google.code.gson:gson:2.10.1'
    compileOnly 'org.eclipse.jgit:org.eclipse.jgit:6.9.0.202403050737-r'

    shadow 'com.google.code.gson:gson:2.10.1'
    shadow 'org.eclipse.jgit:org.eclipse.jgit:6.9.0.202403050737-r'
}

test {
    useJUnitPlatform()
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
}

jar {
    dependsOn configurations.shadow
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.shadow.collect { zipTree(it) }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// ðŸ”§ THIS is what makes Gradle publish the plugin marker artifact
gradlePlugin {
    plugins {
        mml {
            id = "org.mangorage.mangomodloadergradle"
            implementationClass = "org.mangorage.mmlg.MMLGradlePlugin"
            displayName = "MangoModLoader Gradle Plugin"
            description = "Gradle plugin for generating and managing MangoModLoader sources."
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://maven.mangorage.org/releases/"
            credentials {
                username = "mango"
                password = "$MAVEN_PASSWORD"
            }
        }
    }
}